# Maintainer: David Grayson <davidegrayson@gmail.com>

# NOTE: cffi's tests require pytest to run and we don't have a package
# for that yet, so we don't run the tests.
# NOTE: When I install pytest with pip and run the tests manually, there
# are failures and a segmentation fault.

# NOTE: cffi's documentation says that pycparser is a dependency of it,
# but we don't have an MSYS2 package for that yet.

_realname=cffi
pkgbase=mingw-w64-python-${_realname}
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-python2-$_realname"
  "${MINGW_PACKAGE_PREFIX}-python3-$_realname"
)
pkgver=1.4.1
pkgrel=1
pkgdesc="Python 2 and 3 compatibility utilities (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/${_realname}/"
license=('MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python2"
  "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python3"
  "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
  "${MINGW_PACKAGE_PREFIX}-libffi"
)
source=(
  "http://pypi.python.org/packages/source/c/${_realname}/${_realname}-${pkgver}.tar.gz"
  "01-dont-build-libffi.patch"
  "02-dont-def-ffi-prep-closure.patch"
)
sha256sums=('61fa9e32cc91744b92b3ec96f74c5910aa6c2f0a9fbba939fbad71dab558b974'
            '956224f6f828ca7c2ff35c1122e869a600db519d2c38265b695586cad7fd74bb'
            'fe181ef9118b458445f172e1c1dd70e3407865d2544aa28b77f22fbad4a3aff9')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  rm -r c/libffi_msvc
  patch -p1 -i ../01-dont-build-libffi.patch
  patch -p1 -i ../02-dont-def-ffi-prep-closure.patch

  cd "${srcdir}"
  rm -rf python2-build python3-build
  cp -r "${_realname}-${pkgver}" python2-build
  cp -r "${_realname}-${pkgver}" python3-build
}

build() {
  for n in 2 3; do
    cd "${srcdir}/python${n}-build"
    "${MINGW_PREFIX}/bin/python${n}" setup.py build
  done
}

package() {
  local n=$1

  depends=(
    "${MINGW_PACKAGE_PREFIX}-python${n}"
    "${MINGW_PACKAGE_PREFIX}-libffi"
  )

  cd "${srcdir}/python${n}-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  "${MINGW_PREFIX}/bin/python${n}" setup.py install \
    --prefix="${MINGW_PREFIX}" \
    --root="${pkgdir}" \
    --optimize=1

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python${n}-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-cffi() {
  package 2
}

package_mingw-w64-i686-python3-cffi() {
  package 3
}

package_mingw-w64-x86_64-python2-cffi() {
  package 2
}

package_mingw-w64-x86_64-python3-cffi() {
  package 3
}
